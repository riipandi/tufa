apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'
apply plugin: 'com.google.gms.google-services'

def getCmdOutput = { cmd ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine cmd
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def getGitHash = { -> return getCmdOutput(["git", "rev-parse", "--short", "HEAD"]) }
def getGitBranch = { -> return getCmdOutput(["git", "rev-parse", "--abbrev-ref", "HEAD"]) }

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "dev.altaris.tufa"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "0.1.0"
        multiDexEnabled true
        buildConfigField "String", "GIT_HASH", "\"${getGitHash()}\""
        buildConfigField "String", "GIT_BRANCH", "\"${getGitBranch()}\""
    }

    lintOptions {
        abortOnError true
        disable "MissingTranslation"
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()

            ignoreFailures false
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
            }
        }
    }

    buildTypes {
        debug {
            // applicationIdSuffix ".debug"
            manifestPlaceholders = [title: "TufaDev", iconName: "ic_launcher_debug"]
            resValue "bool", "pref_secure_screen_default", "false"
            postprocessing {
                removeUnusedCode true
                removeUnusedResources true
                obfuscate false
                optimizeCode false
                proguardFiles getDefaultProguardFile('proguard-defaults.txt'), 'proguard-rules.pro'
            }
        }
        release {
            manifestPlaceholders = [title: "Tufa", iconName: "ic_launcher"]
            resValue "bool", "pref_secure_screen_default", "true"
            postprocessing {
                removeUnusedCode true
                removeUnusedResources true
                obfuscate false
                optimizeCode true
                proguardFiles getDefaultProguardFile('proguard-defaults.txt'), 'proguard-rules.pro'
            }
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.8.0'
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}

dependencies {
    def libsuVersion = '2.5.1'
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.google.protobuf:protobuf-javalite:3.12.2'
    implementation 'com.google.firebase:firebase-analytics:17.4.2'
    implementation 'net.lingala.zip4j:zip4j:2.6.0'
    implementation 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation "androidx.biometric:biometric:1.0.1"
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'com.google.guava:guava:29.0-jre'
    implementation 'com.getbase:floatingactionbutton:1.10.1'
    implementation 'com.github.apl-devs:appintro:6.0.0'
    implementation 'com.github.avito-tech:krop:0.44'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.65.01'
    implementation 'de.hdodenhof:circleimageview:3.1.0'
    implementation 'me.dm7.barcodescanner:zxing:1.9.8'
    implementation "com.github.topjohnwu.libsu:core:${libsuVersion}"
    implementation "com.github.topjohnwu.libsu:io:${libsuVersion}"
    implementation 'com.github.bumptech.glide:annotations:4.11.0'
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    implementation "com.mikepenz:iconics-core:3.2.5"
    implementation 'com.mikepenz:material-design-iconic-typeface:2.2.0.5@aar'
    implementation 'de.psdev.licensesdialog:licensesdialog:2.1.0'
    implementation('com.github.bumptech.glide:recyclerview-integration:4.11.0') {
        transitive = false
    }
    annotationProcessor 'androidx.annotation:annotation:1.1.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
    testImplementation 'com.google.guava:guava:28.2-jre"'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
}
